; blink sample for CH32V003F4P6 MCU with a LED connected to PD2 using timer and interrupt
; command line to build the sample: a1rv32 -d -f -a -m CH32V003F4 blink1.asm

.STACK
DB (0x200)

.CODE INIT
I32.J __START
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __UNHANDLED
I32.J __TIM2_INT_HNDL

:__UNHANDLED
J __UNHANDLED

:__START
; initialize stack
LI SP, __DATA_START + __DATA_TOTAL_SIZE

; select vectored interrupts mode
CSRRWI ZERO, MTVEC, MTVEC_MODE_VECTORED | MTVEC_VTID_JMP

; enable port D module clock
LW A0, RCC_APB2PCENR
ORI A0, A0, RCC_APB2PCENR_IOPDEN
SW A0, RCC_APB2PCENR

; configure PD2 for output (50 MHz, push-pull)
LW A0, GPIOD_CFGLR
ANDI A0, A0, 0xFFFFF0FF
ORI A0, A0, 3 << 8
SW A0, GPIOD_CFGLR

; enable interrupts globally
CSRRWI ZERO, MSTATUS, MSTATUS_MIE

; enable TIM2 timer module clock
LW A0, RCC_APB1PCENR
ORI A0, A0, RCC_APB1PCENR_TIM2EN
SW A0, RCC_APB1PCENR

; initialize timer
LI A0, 1000 ; 1000 ms
SH A0, TIM2_ATRLR
LI A0, 7999 ; the MCU starts at 8 MHz clock speed
SH A0, TIM2_PSC
; clear UIF flag
SH ZERO, TIM2_INTFR
; enable TIM2 interrupt
LW A0, PFIC_IENR2
ORI A0, A0, PFIC_IENR2_TIM2
SW A0, PFIC_IENR2
; enable TIM2 update interrupt
LI A0, TIM2_DMAINTENR_UIE
SH A0, TIM2_DMAINTENR
; enable timer
LI A0, TIM2_CTLR1_CEN
SH A0, TIM2_CTLR1

; just a loop
:__WAIT_LOOP
WFI
J __WAIT_LOOP

; TIM2 timer interrupt handler
:__TIM2_INT_HNDL
ADDI SP, SP, -16
SW A0, 0(SP)
SW T0, 4(SP)
; check for update interrupt
LHU A0, TIM2_INTFR
ANDI A0, A0, TIM2_INTFR_UIF
BEQ A0, ZERO, __LBL_NOT_UPD_INT
;toggle PD2 pin
LHU A0, GPIOD_OUTDR
XORI A0, A0, 1 << 2
SH A0, GPIOD_OUTDR
; clear interrupt flag
LHU A0, TIM2_INTFR
ANDI A0, A0, !TIM2_INTFR_UIF
SH A0, TIM2_INTFR

:__LBL_NOT_UPD_INT
LW T0, 4(SP)
LW A0, 0(SP)
ADDI SP, SP, 16
MRET
