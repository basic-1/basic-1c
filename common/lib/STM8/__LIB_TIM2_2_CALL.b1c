; TIM2 PERIODMS <value>
; X - period in ms

INI,__LIB_CPU_OPT

:__LIB_TIM2_2_CALL
ASM
; PERIODMS
LD A, (__LIB_CPU_OPT)
CP A, 3 ; LSI
JREQ ::__LBL_PERIODMS_LSI
; HSI or HSE
CLR A
CPW X, 100
RLC A ; A = (X < 100) ? 1 : 0
PUSH A

; period_ms * 125
PUSHW X
CLRW Y

LD A, 7
:::__LBL_PERIODMS_MUL128
SLAW X
RLCW Y
DEC A
JRNE ::__LBL_PERIODMS_MUL128

LD A, 3
:::__LBL_PERIODMS_SUB3
SUBW X, (1, SP)
JRNC ::__LBL_NO_CARRY
DECW Y
:::__LBL_NO_CARRY
DEC A
JRNE ::__LBL_PERIODMS_SUB3

ADDW SP, 2
POP A
TNZ A
JREQ ::__LBL_PERIODMS_GE100

LD A, 7 ; DIV128
BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_16MHZ
; 8 MHz
DEC A ; DIV64
:::__LBL_PERIODMS_16MHZ
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_GE100
; Y:X = Y:X / 256
SWAPW X
RRWA Y
LD XH, A

BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_16MHZ_1
; 8 MHz
; Y:X = Y:X / 2
SRLW Y
RRCW X

:::__LBL_PERIODMS_16MHZ_1
LD A, 15 ; DIV32768
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_LSI
; LSI
CPW X, 100
JRULT ::__LBL_PERIODMS_LSI_LT100
LD A, 7 ; DIV128
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_LSI_LT100
LD A, 32
MUL X, A
LD A, 2 ; DIV4

:::__LBL_PERIODMS_SET_REGS
LD (TIM2_PSCR), A
DECW X
LD A, XH
LD (TIM2_ARRH), A
LD A, XL
LD (TIM2_ARRL), A
RET
ENDASM