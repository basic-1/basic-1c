; TIM2 PERIODMS <value>
; X - period in ms

INI,__LIB_CPU_OPT

:__LIB_TIM2_2_CALL
ASM
LD A, (__LIB_CPU_OPT)
CP A, 3
JREQ ::__LBL_PERIODMS_LSI ; 128kHz LSI
; HSI or HSE
CLR A
CPW X, 100
RLC A ; A = (X < 100) ? 1 : 0
PUSH A

; period_ms * 125
PUSHW X

LD A, 7

; Y:X = X << A
CLRW Y

:::__LBL_DO_SHIFT
SLAW X
RLCW Y
DEC A
JRNE ::__LBL_DO_SHIFT

LD A, 3
:::__LBL_PERIODMS_SUB3
SUBW X, (1, SP)
JRNC ::__LBL_NO_CARRY
DECW Y
:::__LBL_NO_CARRY
DEC A
JRNE ::__LBL_PERIODMS_SUB3

ADDW SP, 2

POP A
TNZ A
JREQ ::__LBL_PERIODMS_GE100

LD A, TIM2_PSCR_PSC_DIV128 ; DIV128
BTJF (__LIB_CPU_OPT), 1, ::__LBL_PERIODMS_16_OR_8MHZ
; 24 MHz
DEC A ; DIV = DIV / 2 (64)
PUSHW X ; X = X * 3
SLLW X
ADDW X, (1, SP)
ADDW SP, 2

:::__LBL_PERIODMS_16_OR_8MHZ
BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_SET_REGS
; 8 MHz
DEC A ; DIV = DIV / 2 (64)
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_GE100
; Y:X = Y:X / 256
SWAPW X
RRWA Y
LD XH, A

BTJF (__LIB_CPU_OPT), 1, ::__LBL_PERIODMS_16_OR_8MHZ_1
; 24 MHz
PUSHW X ; X = X * 1.5
SRLW X
ADDW X, (1, SP)
ADDW SP, 2

:::__LBL_PERIODMS_16_OR_8MHZ_1
BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_16MHZ_1
; 8 MHz
; X = X / 2
SRLW X

:::__LBL_PERIODMS_16MHZ_1
LD A, TIM2_PSCR_PSC_DIV32768 ; DIV32768
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_LSI
; 128 kHz LSI
LD A, TIM2_PSCR_PSC_DIV128 ; DIV128
CPW X, 100
JRUGE ::__LBL_PERIODMS_SET_REGS
LD A, 32
MUL X, A
LD A, TIM2_PSCR_PSC_DIV4 ; DIV4

:::__LBL_PERIODMS_SET_REGS
LD (TIM2_PSCR), A
DECW X
LD A, XH
LD (TIM2_ARRH), A
LD A, XL
LD (TIM2_ARRL), A
RET
ENDASM