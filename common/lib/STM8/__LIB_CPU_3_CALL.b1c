; CPU DELAYMS
; A - delay time in milliseconds
INI,__LIB_CPU_OPT

:__LIB_CPU_3_CALL
ASM
TNZ A
JREQ ::__LBL_EXIT

.IF SUBSTR(__MCU_NAME,0,5) == STM8S
LDW X, 44 ; 128 kHz
BTJT (__LIB_CPU_OPT), 1, ::__LBL_DO_DELAY
LDW X, 42 * 8 * 16 ; 16 MHz
BTJF (__LIB_CPU_OPT), 0, ::__LBL_DO_DELAY
SRLW X ; 8 MHz
.ELSE ; STM8L family
BTJT (__LIB_CPU_OPT), 1, ::__LBL_LS_OSC
; high-speed oscillator
LDW X, 42 * 8 * 16 ; 16 MHz
BTJF (__LIB_CPU_OPT), 0, ::__LBL_DO_DELAY
SRLW X ; 8 MHz
JRA ::__LBL_DO_DELAY

:::__LBL_LS_OSC
LDW X, 10 ; 32.768 kHz
BTJF (__LIB_CPU_OPT), 0, ::__LBL_LSE_CORRECT
INCW X ; 38 kHz

:::__LBL_LSE_CORRECT
PUSHW X
SUBW X, 8
JRA ::__LBL_1_MS_LOOP
.ENDIF

:::__LBL_DO_DELAY
PUSHW X

:::__LBL_DELAY_LOOP
LDW X, (1, SP) ; 2

:::__LBL_1_MS_LOOP
DECW X ; 1
JRNE ::__LBL_1_MS_LOOP ; 1/2
DEC A ; 1
JRNE ::__LBL_DELAY_LOOP ; 1/2
POPW X

:::__LBL_EXIT
RET
ENDASM
