; TIM5 PERIODMS <value>
; X - period in ms

INI,__LIB_CPU_OPT

:__LIB_TIM5_2_CALL
ASM
; PERIODMS
BTJT (__LIB_CPU_OPT), 1, ::__LBL_PERIODMS_LS ; LSI or LSE
; HSI or HSE
.IF SUBSTR(__MCU_NAME,0,5) == STM8S ; use STM8S 4-bit prescaler to allow wider period range
CLR A
CPW X, 100
RLC A ; A = (X < 100) ? 1 : 0
PUSH A
.ENDIF

; period_ms * 125
PUSHW X

LD A, 7
CALLR ::__LIB_TIM5_2_MUL2

LD A, 3
:::__LBL_PERIODMS_SUB3
SUBW X, (1, SP)
JRNC ::__LBL_NO_CARRY
DECW Y
:::__LBL_NO_CARRY
DEC A
JRNE ::__LBL_PERIODMS_SUB3

ADDW SP, 2

.IF SUBSTR(__MCU_NAME,0,5) == STM8S ; wider period range
POP A
TNZ A
JREQ ::__LBL_PERIODMS_GE100
.ENDIF

LD A, TIM5_PSCR_PSC_DIV128 ; DIV128
BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_SET_REGS
; 8 MHz
.IF SUBSTR(__MCU_NAME,0,5) == STM8S
DEC A ; DIV64
.ELSE
SRLW Y
RRCW X ; for STM8L use the same DIV128 prescaler value but divide counter by 2
.ENDIF
JRA ::__LBL_PERIODMS_SET_REGS

.IF SUBSTR(__MCU_NAME,0,5) == STM8S ; wider period range
:::__LBL_PERIODMS_GE100
; Y:X = Y:X / 256
SWAPW X
RRWA Y
LD XH, A

BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_16MHZ_1
; 8 MHz
; Y:X = Y:X / 2
SRLW Y
RRCW X

:::__LBL_PERIODMS_16MHZ_1
LD A, TIM5_PSCR_PSC_DIV32768 ; DIV32768
JRA ::__LBL_PERIODMS_SET_REGS
.ENDIF

:::__LBL_PERIODMS_LS
; LSI or LSE
.IF SUBSTR(__MCU_NAME,0,5) == STM8S ; 128 kHz LSI
CPW X, 100
JRULT ::__LBL_PERIODMS_LSI_LT100
LD A, TIM5_PSCR_PSC_DIV128 ; DIV128
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_LSI_LT100
LD A, 32
MUL X, A
LD A, TIM5_PSCR_PSC_DIV4 ; DIV4
.ELSE
BTJF (__LIB_CPU_OPT), 0, ::__LBL_PERIODMS_LSE
; 38 kHz LSI
; period_ms * 19
PUSHW X

LD A, 4
CALLR ::__LIB_TIM5_2_MUL2

LD A, 3
:::__LBL_PERIODMS_ADD3
ADDW X, (1, SP)
JRNC ::__LBL_NO_CARRY_1
INCW Y
:::__LBL_NO_CARRY_1
DEC A
JRNE ::__LBL_PERIODMS_ADD3
ADDW SP, 2

TNZW Y
JREQ ::__LBL_PERIODMS_LSI_LT3450
LD A, 5
:::__LBL_PERIODMS_DIV32
SRLW Y
RRCW X
DEC A
JRNE ::__LBL_PERIODMS_DIV32

LD A, TIM5_PSCR_PSC_DIV64 ; DIV64
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_LSI_LT3450
LD A, TIM5_PSCR_PSC_DIV2 ; DIV2
JRA ::__LBL_PERIODMS_SET_REGS

:::__LBL_PERIODMS_LSE
; 32.768 kHz LSE
CPW X, 2000
JRUGE ::__LBL_PERIODMS_LSE_GE2000
PUSH TIM5_PSCR_PSC_DIV1 ; DIV1
LD A, 15 ; * 32768
JRA ::__LBL_PERIODMS_MULDIV1k

:::__LBL_PERIODMS_LSE_GE2000
PUSH TIM5_PSCR_PSC_DIV64 ; DIV64
LD A, 9 ; * 512

:::__LBL_PERIODMS_MULDIV1k
CALLR ::__LIB_TIM5_2_MUL2
CALLR __LIB_AUX_DIV10
CALLR __LIB_AUX_DIV10
CALLR __LIB_AUX_DIV10
POP A
.ENDIF

:::__LBL_PERIODMS_SET_REGS
LD (TIM5_PSCR), A
DECW X
LD A, XH
LD (TIM5_ARRH), A
LD A, XL
LD (TIM5_ARRL), A
RET

; Y:X = X << A
:::__LIB_TIM5_2_MUL2
CLRW Y

:::__LBL_DO_SHIFT
SLAW X
RLCW Y
DEC A
JRNE ::__LBL_DO_SHIFT
RET
ENDASM