; CPU CLOCKSOURCE HSI/HSI24/HSI48/HSI16/HSI8/HSE24/HSE48
; HSE24 and HSE48 requires 24MHz external crystal resonator
; HSI16, HSI48 and HSE48 use PLL to double oscillator's frequency
; A1 - predefined value specific to clock source
; bits 0-1 - SW (HSI or HSE)
; bits 2-3 - SWS (HSI or HSE)
; bits 4-7 - AHB prescaler value
; bit 8 - HSI (1) or HSE (0)
; bit 9 - use PLL
; bit 10 - set if SYSCLK > 24 MHz
; bits 16..23 - value for __LIB_CPU_OPT variable

INI,__LIB_CPU_OPT

:__LIB_CPU_1_CALL
ASM
; enable HSI or HSE
ANDI A0, A1, 0x100
LI T1, RCC_CTLR_HSEON
LI T2, RCC_CTLR_HSERDY
BEQ A0, ZERO, ::__LBL_ENA_OSC
LI T1, RCC_CTLR_HSION
LI T2, RCC_CTLR_HSIRDY
:::__LBL_ENA_OSC
LW A0, RCC_CTLR
OR A0, A0, T1
SW A0, RCC_CTLR
:::__LBL_WAIT_ENA_OSC
LW A0, RCC_CTLR
AND A0, A0, T2
BEQ A0, ZERO, ::__LBL_WAIT_ENA_OSC

; set HSI or HSE as SYSCLK clock source
LW A0, RCC_CFGR0
ANDI A0, A0, !RCC_CFGR0_SW
ANDI T1, A1, RCC_CFGR0_SW
OR A0, A0, T1
SW A0, RCC_CFGR0
ANDI T1, A1, RCC_CFGR0_SWS
:::__LBL_WAIT_SWS
LW A0, RCC_CFGR0
AND A0, A0, T1
BNE A0, T1, ::__LBL_WAIT_SWS

; turn off PLL
LW A0, RCC_CTLR
ANDI A0, A0, !RCC_CTLR_PLLON
SW A0, RCC_CTLR
:::__LBL_WAIT_DISA_PLL
LW A0, RCC_CTLR
ANDI A0, A0, RCC_CTLR_PLLRDY
BNE A0, ZERO, ::__LBL_WAIT_DISA_PLL

; check if PLL is used
ANDI A0, A1, 0x200
BEQ A0, ZERO, ::__LBL_SET_FLASH_LAT

; set PLL source
ANDI A0, A1, 0x100
LI T1, RCC_CFGR0_PLLSRC_HSE
BEQ A0, ZERO, ::__LBL_SET_PLL_SRC
LI T1, RCC_CFGR0_PLLSRC_HSI
:::__LBL_SET_PLL_SRC
LW A0, RCC_CFGR0
ANDI A0, A0, !RCC_CFGR0_PLLSRC
OR A0, A0, T1
SW A0, RCC_CFGR0

; enable PLL
LW A0, RCC_CTLR
ORI A0, A0, RCC_CTLR_PLLON
SW A0, RCC_CTLR
:::__LBL_WAIT_ENA_PLL
LW A0, RCC_CTLR
ANDI A0, A0, RCC_CTLR_PLLRDY
BEQ A0, ZERO, ::__LBL_WAIT_ENA_PLL

; set flash latency
:::__LBL_SET_FLASH_LAT
ANDI A0, A1, 0x400
LI T1, FLASH_ACTLR_LATENCY_0
BEQ A0, ZERO, ::__LBL_SET_FLASH_LAT_0
LI T1, FLASH_ACTLR_LATENCY_1 ; flash latency for SYSCLK > 24 MHz
:::__LBL_SET_FLASH_LAT_0
SW T1, FLASH_ACTLR

; set AHB clock source prescaler
LW A0, RCC_CFGR0
ANDI A0, A0, !RCC_CFGR0_HPRE
ANDI T1, A1, RCC_CFGR0_HPRE
OR A0, A0, T1
SW A0, RCC_CFGR0

; set PLL as SYSCLK clock source if necessary
ANDI A0, A1, 0x200
BEQ A0, ZERO, ::__LBL_SAVE_OPT
LW A0, RCC_CFGR0
ANDI A0, A0, !RCC_CFGR0_SW
ORI A0, A0, RCC_CFGR0_SW_PLL
SW A0, RCC_CFGR0
LI T1, RCC_CFGR0_SWS_PLL
:::__LBL_WAIT_SWS_PLL
LW A0, RCC_CFGR0
AND A0, A0, T1
BNE A0, T1, ::__LBL_WAIT_SWS_PLL

; save options
:::__LBL_SAVE_OPT
SRLI A0, A1, 16
SB A0, __LIB_CPU_OPT
RET
ENDASM
